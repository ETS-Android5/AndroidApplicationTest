buildscript {
    def needCustom = false

    ext {
        kotlin_version = '1.5.10'
        shadow_version = 'local-0b373991-SNAPSHOT'
        shadow_repository = 'E:\\repository'

        //定义共享属性
        if (needCustom) {
            group_suffix = '.test'
        } else {
            group_suffix = ''
        }
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"

    api "com.tencent.shadow.dynamic${group_suffix}:host:$shadow_version"
    implementation "com.tencent.shadow.core${group_suffix}:activity-container:$shadow_version"

    implementation "com.tencent.shadow.dynamic${group_suffix}:manager:$shadow_version"
    compileOnly "com.tencent.shadow.core${group_suffix}:common:$shadow_version"
    //    compileOnly "com.tencent.shadow.dynamic${rootProject.ext.group_suffix}:host:$shadow_version"

    // 下面这些依赖主要用于方便调试，compileOnly 编译时可见，不会打包到 apk
    compileOnly "com.tencent.shadow.core${group_suffix}:loader:$shadow_version"
    compileOnly "com.tencent.shadow.dynamic${group_suffix}:loader-impl:$shadow_version"
    compileOnly "com.tencent.shadow.core${group_suffix}:activity-container:$shadow_version"
}

repositories {
    if (!System.getenv().containsKey("DISABLE_TENCENT_MAVEN_MIRROR")) {
        maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
    } else {
        google()
    }
    maven {
        name = "GitHubPackages"
        url "https://maven.pkg.github.com/tencent/shadow"
        //一个只读账号兼容Github Packages暂时不支持匿名下载
        //https://github.community/t/download-from-github-package-registry-without-authentication/14407
        credentials {
            username = 'readonlypat'
            password = '\u0062\u0036\u0064\u0037\u0035\u0032\u0062\u0061\u0035\u0038\u0063\u0064\u0032\u0061\u0038\u0037\u0064\u0033\u0034\u0033\u0039\u0038\u0035\u0036\u0032\u0034\u0065\u0039\u0031\u0036\u0066\u0065\u0065\u0062\u0031\u0065\u0033\u0037\u0061\u0039'
        }
    }
    mavenLocal()

    if (file(shadow_repository).exists()) {
        maven { url = uri(shadow_repository) }
    }
}
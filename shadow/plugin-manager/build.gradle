plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
apply from: '../shadow_common.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.hl.shadow.pluginmanager"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name

            //获取当前时间的"YYYY-MM-dd"格式。
            // def createTime = new Date().format("YYYY-MM-dd", java.util.TimeZone.getTimeZone("GMT+08:00"))

            def fileName = "My-PluginManager_${buildType}.apk"
            outputFileName = fileName

            def outputDirectory = new File(parent.buildDir, buildType)
            println("Manager 打包输出至 ---------> ${outputDirectory.absolutePath}${File.separator}${outputFileName}")
            variant.getPackageApplicationProvider().get().outputDirectory = outputDirectory

            def allTask = tasks.findAll {
                it.name.startsWith('assemble') && (it.name.endsWith("Debug") || it.name.endsWith("Release"))
            }

            // 给相关打包程序添加复制任务
            allTask.each {
                it.doLast {
                    println "正在将${fileName}拷贝至 App 资源目录下"
                    // 复制至 app 资源目录下
                    copy {
                        from "${outputDirectory.absolutePath}${File.separator}${outputFileName}"
                        into "../../application/app/src/main/assets/plugin"
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.tencent.shadow.dynamic${group_suffix}:host:$shadow_version"

    implementation "com.tencent.shadow.core${group_suffix}:activity-container:$shadow_version"

    implementation "com.tencent.shadow.dynamic${group_suffix}:manager:$shadow_version"
    compileOnly "com.tencent.shadow.core${group_suffix}:common:$shadow_version"
    //    compileOnly "com.tencent.shadow.dynamic${rootProject.ext.group_suffix}:host:$shadow_version"

    // 下面这些依赖主要用于方便调试，compileOnly 编译时可见，不会打包到 apk
    compileOnly "com.tencent.shadow.core${group_suffix}:loader:$shadow_version"
    compileOnly "com.tencent.shadow.dynamic${group_suffix}:loader-impl:$shadow_version"
    compileOnly "com.tencent.shadow.core${group_suffix}:activity-container:$shadow_version"
}